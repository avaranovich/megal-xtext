grammar org.softlang.megal.language.Megal hidden(WS, COMMENT)

import "http://softlang.wikidot.com/megal"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

MegalFile:
	annotations+=MegalAnnotation* 'model' name=ID
	('import' imports+=[MegalFile]
	| declarations+=MegalEntityType
	| declarations+=MegalRelationshipType
	| declarations+=MegalEntity
	| declarations+=MegalRelationship
	| declarations+=MegalPair
	| bindings+=MegalLink)*;

MegalAnnotation:
	'@' key=ID (value=STR)?;

MegalLink:
	annotations+=MegalAnnotation* link=[MegalEntity]
	('(' first=[MegalEntity] ')' '|->'
	second=[MegalEntity])? '=' to=STR;

MegalEntityType:
	annotations+=MegalAnnotation* name=ID
	('<' supertype=[MegalEntityType] | 'as' 'entity');

MegalRelationshipType:
	annotations+=MegalAnnotation* name=ID '<'
	// Part of left type assignment
	left=[MegalEntityType]
	('[' leftParams+=[MegalEntity]
	('->' leftParams+=[MegalEntity])*
	']')?
	(leftBoth?='(+)' | leftMany?='+')?
	'*'
	// Part of right type assignment
	right=[MegalEntityType]
	('[' rightParams+=[MegalEntity]
	('->' rightParams+=[MegalEntity])*
	']')?
	(rightBoth?='(+)' | rightMany?='+')?;

MegalEntity:
	annotations+=MegalAnnotation* name=ID ':'
	// Part of type assignment
	type=[MegalEntityType]
	('[' params+=[MegalEntity]
	('->' params+=[MegalEntity])*
	']')? many?='+'?;

MegalRelationship:
	annotations+=MegalAnnotation*
	left=[MegalEntity] type=[MegalRelationshipType] right=[MegalEntity];

MegalPair:
	annotations+=MegalAnnotation* set=[MegalEntity]
	'(' first=[MegalEntity] ')' '|->' second=[MegalEntity];

ID:
	NAME ('::' NAME)*;

NAME:
	STR | (UCID | LCID) ('.' (UCID | LCID))*;

terminal UCID:
	'^'? ('A'..'Z' | '0'..'9') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal LCID:
	'^'? ('a'..'z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal STR:
	"'" ('\\' . | !('\\' | "'"))* "'";

terminal COMMENT:
	'/*'->'*/' | '//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;
