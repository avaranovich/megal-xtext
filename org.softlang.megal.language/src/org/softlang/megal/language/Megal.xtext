grammar org.softlang.megal.language.Megal hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://softlang.wikidot.com/megal"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

MegalFile:
	annotations+=PrefixAnnotation* 'model' name=QID
	('import' imports+=[MegalFile|QID] | declarations+=MegalDeclaration | bindings+=MegalLink)*;

MegalDeclaration:
	MegalEntityType | MegalRelationshipType | MegalEntity | MegalRelationship | MegalFunctionApplication;

PrefixAnnotation returns MegalAnnotation:
	'@' key=QID (value=STRING)?;

ShowNameAnnotation returns MegalAnnotation:
	'[' value=STRING ']';

MegalLink:
	annotations+=PrefixAnnotation*
	link=[MegalEntity|QID] ('(' input=[MegalEntity|QID] ')' '|' '->' output=[MegalEntity|QID])? '=' to=STRING;

MegalEntityType:
	annotations+=PrefixAnnotation*
	name=QID
	(annotations+=ShowNameAnnotation)?
	('<' supertype=[MegalEntityType|QID] | 'as' 'entity');

MegalRelationshipType:
	annotations+=PrefixAnnotation*
	name=QID
	(annotations+=ShowNameAnnotation)?
	'<'
	// Part of left type assignment
	typeLeft=[MegalEntityType|QID] ('[' typeLeftParameters+=[MegalEntity|QID] ('->' typeLeftParameters+=[MegalEntity|QID])*
	']')?
	(typeLeftArb?='(' typeLeftMany?='+' ')' | typeLeftMany?='+')?
	// Operator
	'*'
	// Part of right type assignment
	typeRight=[MegalEntityType|QID] ('[' typeRightParameters+=[MegalEntity|QID] ('->'
	typeRightParameters+=[MegalEntity|QID])* ']')?
	(typeRightArb?='(' typeRightMany?='+' ')' | typeRightMany?='+')?;

MegalEntity:
	annotations+=PrefixAnnotation*
	(dependent?='?' | parameter?='!')?
	name=QID
	(annotations+=ShowNameAnnotation)?
	':'
	// Part of type assignment
	type=[MegalEntityType|QID] ('[' typeParameters+=[MegalEntity|QID] ('->' typeParameters+=[MegalEntity|QID])* ']')?
	typeMany?='+'?;

MegalRelationship:
	annotations+=PrefixAnnotation*
	left=[MegalEntity|QID]
	type=[MegalRelationshipType|QID]
	right=[MegalEntity|QID];

MegalFunctionApplication:
	annotations+=PrefixAnnotation*
	function=[MegalEntity|QID]
	'(' input=[MegalEntity|QID] ')' '|' '->' output=[MegalEntity|QID];

QID:
	NAME ('::' NAME)*;

NAME:
	STRING | ID ('.' ID)*;

ID:
	UCID | LCID;
	/**
	 * Upper case identifier, most likely an entity
	 */
terminal UCID:
	'^'? ('A'..'Z' | '0'..'9') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

	/**
	 * Lower case identifier, most likely a relationship
	 */
terminal LCID:
	'^'? ('a'..'z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

	/**
	 * String, for annotation values
	 */
terminal STRING:
	'"' ('\\' . | !('\\' | '"'))* '"' |
	"'" ('\\' . | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

	/**
	 * Segment of an URI
	 */
terminal fragment SEGMENT:
	('A'..'Z' | 'a'..'z' | '0'..'9' | '-' | '.' | '_' | '~' | ':' | '?' | '#' | '[' | ']' | '@' | '!' | '$' | '&' | "'" |
	'(' | ')' | '*' | '+' | ',' | ';' | '=')+;