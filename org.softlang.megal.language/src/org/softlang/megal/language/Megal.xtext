grammar org.softlang.megal.language.Megal hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://softlang.wikidot.com/megal"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Megamodel:
	info+=PrefixAnnotation* 'model' name=QID
	('import' imports+=[Megamodel|QID] | declarations+=Declaration | bindings+=Link)*;

Declaration:
	EntityType | RelationshipType | Entity | Relationship | FunctionApplication;

PrefixAnnotation returns Annotation:
	'@' key=QID (value=STRING)?;

ShowNameAnnotation returns Annotation:
	'[' value=STRING ']';

Link:
	info+=PrefixAnnotation*
	link=[Entity|QID] '=' to=URI;

EntityType:
	info+=PrefixAnnotation*
	name=QID
	(info+=ShowNameAnnotation)?
	('<' supertype=EntityTypeReference | 'as' 'entity');

RelationshipType:
	info+=PrefixAnnotation*
	name=QID
	(info+=ShowNameAnnotation)?
	'<' left=EntityTypeReference
	'*' right=EntityTypeReference;

Entity:
	info+=PrefixAnnotation*
	(dependent?='?' | parameter?='!')?
	name=QID
	(info+=ShowNameAnnotation)?
	':' type=EntityTypeReference;

Relationship:
	info+=PrefixAnnotation*
	left=[Entity|QID]
	type=[RelationshipType|QID]
	right=[Entity|QID];

FunctionApplication:
	info+=PrefixAnnotation*
	function=[Entity|QID]
	'(' input=[Entity|QID] ')' '|' '->' output=[Entity|QID];

EntityTypeReference:
	definition=[EntityType|QID]
	('[' parameters+=[Entity|QID] ('->' parameters+=[Entity|QID])* ']')?
	(many?='+')?;

QID:
	NAME (QNS NAME)*;

NAME:
	ID ('.' ID)*;

ID:
	UCID | LCID;
	/**
	 * Upper case identifier, most likely an entity
	 */
terminal UCID:
	'^'? ('A'..'Z' | '0'..'9') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

	/**
	 * Lower case identifier, most likely a relationship
	 */
terminal LCID:
	'^'? ('a'..'z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

	/**
	 * String, for annotation values
	 */
terminal STRING:
	'"' ('\\' . | !('\\' | '"'))* '"' |
	"'" ('\\' . | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

	/**
	 * Segment of an URI
	 */
terminal fragment SEGMENT:
	('A'..'Z' | 'a'..'z' | '0'..'9' | '-' | '.' | '_' | '~' | ':' | '?' | '#' | '[' | ']' | '@' | '!' | '$' | '&' | "'" |
	'(' | ')' | '*' | '+' | ',' | ';' | '=')+;

terminal QNS:
	'::';

	/**
	 * An URI
	 */
terminal URI returns URI:
	('a'..'z')+ ':/' '/'? SEGMENT ('/' SEGMENT)* '/'?;