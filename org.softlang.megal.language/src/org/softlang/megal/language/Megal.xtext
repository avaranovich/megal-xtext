grammar org.softlang.megal.language.Megal hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://softlang.wikidot.com/megal"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Megamodel:
	'model' name=QID
	('import' imports+=[Megamodel|QID] | declarations+=Declaration | bindings+=Link)*;

Declaration:
	EntityType | RelationshipType | Entity | Relationship | FunctionApplication;

Link:
	link=[Entity|QID] '=' to=STRING;

PrefixAnnotation returns Annotation:
	'@' key=QID (value=STRING)?;

ShowNameAnnotation returns Annotation:
	'[' value=STRING ']';

EntityType:
	annotations+=PrefixAnnotation*
	name=QID
	(annotations+=ShowNameAnnotation)?
	('<' supertype=EntityTypeReference | 'as' 'entity');

RelationshipType:
	annotations+=PrefixAnnotation*
	name=QID
	(annotations+=ShowNameAnnotation)?
	'<' left=EntityTypeReference
	'*' right=EntityTypeReference;

Entity:
	annotations+=PrefixAnnotation*
	(dependent?='?')?
	(parameter?='!')?
	name=QID
	(annotations+=ShowNameAnnotation)?
	':' type=EntityTypeReference;

Relationship:
	annotations+=PrefixAnnotation*
	left=[Entity|QID]
	type=[RelationshipType|QID]
	right=[Entity|QID];

FunctionApplication:
	function=[Entity|QID]
	'(' input=[Entity|QID] ')' '|' '->' output=[Entity|QID];

EntityTypeReference:
	definition=[EntityType|QID]
	('[' parameters+=[Entity|QID] ('->' parameters+=[Entity|QID])* ']')?
	(many?='+')?;

QID:
	ID ('.' ID)*;

ID:
	UCID | LCID;

	/**
	 * Upper case identifier, most likely an entity
	 */
terminal UCID:
	'^'? ('A'..'Z') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

	/**
	 * Lower case identifier, most likely a relationship
	 */
terminal LCID:
	'^'? ('a'..'z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

	/**
	 * String, for annotation values
	 */
terminal STRING:
	'"' ('\\' . | !('\\' | '"'))* '"' |
	"'" ('\\' . | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;