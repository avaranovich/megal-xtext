grammar org.softlang.megal.MegaL hidden(ML_COMMENT, SL_COMMENT, WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.softlang.org/megal/PP" as pp
generate megaL "http://www.softlang.org/megal/MegaL"

Model:
	MegaLDefinition | MegaLLinking;

MegaLDefinition:
	'model' name=QualifiedID
	('linked' 'in' linker=[MegaLLinking|QualifiedID])?
	(imports+=Import | etds+=ETD | rtds+=RTD | eds+=ED | rds+=RD)*;

Import:
	'import' ref=[MegaLDefinition];


NamedDefinition:
	ETD | RTD | ED;

Annotation:
	'@' key=ID '(' value=STRING ')';

ETD:
	annotations+=Annotation* name=ID '<' supertype=UseETD;

RTD:
	annotations+=Annotation* name=ID '<' domain=UseETD '*' coDomain=UseETD;

RD:
	source=[ED] rel=[RTD] target=[ED];

ED:
	name=ID ':' type=UseETD;

UseETD:
	UseETDRef | UseEntity;

UseETDRef:
	ref=[ETD];

UseEntity:
	{UseEntity} 'Entity';

MegaLLinking:
	'linking' name=QualifiedID
	('for' target=[MegaLDefinition|QualifiedID])?
	(jars+=Jar | providers+=Provider | lds+=LD)*;

Jar:
	'jar' ref=STRING;

Provider:
	'provider' classifier=[pp::Classifier|QualifiedID];

LD:
	target=[ED] '=' value=STRING;

QualifiedID:
	ID ('.' ID)*;

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"';

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;