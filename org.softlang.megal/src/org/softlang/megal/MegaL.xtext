grammar org.softlang.megal.MegaL with org.eclipse.xtext.common.Terminals

generate megaL "http://www.softlang.org/megal/MegaL"

Model:
	MegaLDefinition | MegaLLinking;

MegaLDefinition:
	'model' name=QualifiedID
	('linked' 'in' linker=[MegaLLinking|QualifiedID])?
	(imports+=Import | etds+=ETD | rtds+=RTD | eds+=ED | rds+=RD)*;

Import:
	'import' ref=[MegaLDefinition];

ETDR:
	ETDEntity | ETDDeclared;

ETDEntity:
	{ETDEntity} 'Entity';

ETDDeclared:
	ref=[ETD];

ETD:
	name=ID '<' supertype=ETDR;

RTD:
	name=ID '<' domain=ETDR '*' coDomain=ETDR;

ED:
	name=ID ':' type=ETDR;

RD:
	source=[ED] rel=[RTD] target=[ED];

MegaLLinking:
	'linking' name=QualifiedID
	('for' target=[MegaLDefinition|QualifiedID])?
	(lds+=LD)*;

LD:
	target=[ED] '=' value=STRING;

QualifiedID:
	ID ('.' ID)*;