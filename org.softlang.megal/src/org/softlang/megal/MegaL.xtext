grammar org.softlang.megal.MegaL with org.eclipse.xtext.common.Terminals

generate megaL "http://www.softlang.org/megal/MegaL"

Model:
	MegaLDefinition | MegaLLinking;

MegaLDefinition:
	'model' name=QualifiedID
	('linked' 'in' linker=[MegaLLinking|QualifiedID])?
	(imports+=Import | rds+=RD | etds+=ETD | rtds+=RTD | eds+=EDGroup)*;

Import:
	'import' ref=[MegaLDefinition];

ETD:
	'define' name=ID ('from' supertype=[ETD])?;

RTD:
	'define' name=ID ':' domain=[ETD] '->' coDomain=[ETD];

EDGroup:
	type=[ETD] ':' items+=ED (',' items+=ED)*;

ED:
	name=ID;

RD:
	source=[ED] rel=[RTD] target=[ED];

MegaLLinking:
	'linking' name=QualifiedID
	('for' target=[MegaLDefinition|QualifiedID])?
	(lds+=LD)*;

LD:
	target=[ED] '=' value=STRING;

QualifiedID:
	ID ('.' ID)*;