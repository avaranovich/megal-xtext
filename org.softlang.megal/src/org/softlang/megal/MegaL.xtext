grammar org.softlang.megal.MegaL with org.eclipse.xtext.common.Terminals

generate megaL "http://www.softlang.org/megal/MegaL"

Model:
	MegaLDefinition | MegaLLinking;

MegaLDefinition:
	'model' name=QualifiedID ('linked' 'in' linker=[MegaLLinking|QualifiedID])? (('import' imports+=[MegaLDefinition]) |
	etd+=ETD | rtd+=RTD | ed+=ED | rd+=RD)*;

ETDR:
	ETDEntity | ETDDeclared;

ETDEntity:
	{ETDEntity} 'Entity';

ETDDeclared:
	reference=[ETD];

ETD:
	name=ID '<' supertype=ETDR;

RTD:
	name=ID '<' domain=ETDR '*' coDomain=ETDR;

ED:
	name=ID ':' type=ETDR;

RD:
	source=[ED] rel=[RTD] target=[ED];

QualifiedID:
	ID ('.' ID)*;

MegaLLinking:
	'linking' name=QualifiedID ('for' target=[MegaLDefinition|QualifiedID])? (links+=LD)*;

LD:
	target=[ED] ('.' key=ID)? '=' value=STRING;